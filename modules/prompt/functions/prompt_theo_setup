#
# Authors:
#   Theo Hultberg <theo@iconara.net>
#

# Load dependencies.
pmodload 'helper'
pmodload 'git'

function prompt_theo_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_theo_ruby {
  if type rbenv > /dev/null; then
    ruby_version=$(rbenv version | sed -e 's/ .*//')
    if [[ $ruby_version =~ '^[[:digit:]]\.[[:digit:]]\.[[:digit:]]' ]]; then
      ruby_version="ruby-$ruby_version"
    fi
    echo $ruby_version
  elif type rvm > /dev/null; then
    ~/.rvm/bin/rvm-prompt
  fi
}

function prompt_theo_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  prompt_opts=(percent subst)

  autoload -Uz add-zsh-hook
  autoload -Uz git-info

  add-zsh-hook precmd prompt_theo_precmd

  zstyle ':prezto:module:git:info:branch' format '%b'
  zstyle ':prezto:module:git:info:keys' format 'branch' '%b'

  PROMPT='%2~%# '
  RPROMPT='%B%F{red}${git_info[branch]}%f%b %B%F{yellow}$(prompt_theo_ruby)%f%b %m %T'
}

prompt_theo_setup "$@"

